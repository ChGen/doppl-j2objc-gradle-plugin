/*
 * Copyright (c) 2015 the authors of j2objc-gradle (see AUTHORS file)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// Based on https://plugins.gradle.org/docs/publish-plugin
buildscript {
    ext.kotlin_version = '1.1.60'

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.2.1'
    compile 'org.codehaus.groovy:groovy-android-gradle-plugin:1.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.11'
}

group = 'co.doppl'
//Changed this a whole number to differentiate it from the core stuff. Confusing.
version = '0.10.0'

test {
    enabled = false
}

project.tasks.all {
    if (it.name == 'publishPluginGroovyDocsJar' || it.name == 'publishPluginJavaDocsJar') {
        it.from(project.file('src/main/resources/'))
    }
}

configurations {
    includeInJar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'gradle'
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url dopplMavenDeploy
        }
    }
}

ext.bintrayGitUrl ='https://github.com/doppllib/doppl-gradle.git'
ext.artifactId = 'gradle'

apply from: "$dopplSharedScriptHome/bintray.gradle"

task buildgenPropertiesFile << {
    //
    description 'Dynamically creates a properties file.'

    // needed for the first pass
    def folder = project.file('src/main/resources');
    if(!folder.exists()){
        folder.mkdirs()
    }

    //write it to a propertiess file
    def props = project.file('src/main/resources/buildgen.properties')
    props.delete()
    props << "buildversion=${version}"

}

processResources.dependsOn buildgenPropertiesFile

compileGroovy.dependsOn = compileGroovy.taskDependencies.values - 'compileJava'
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin